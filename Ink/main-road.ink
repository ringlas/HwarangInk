=== main_road ===
#1
    
    {PrintImg("hwarang")}
    
    Kim Gee Soo followed the road with an even pace. It was summer – the season where the weather could quickly turn from scorching sun to heavy showers, and the nature around him was lush and green. The air was muggy from the last rain and heavy with the fragrance of flowers and the odor of rotting vegetation.
    
    The road was leading to the village of Yo Soo where the cursed maiden lived. The hwarang sighed and once again replayed in his mind the conversation with Magistrate Kang who had sent him on this fool’s errand.
    
    "Help your old friend, Gee Soo! My son is wasting away day after day and is beginning to look and act like a ghost!" the magistrate said but meanwhile he was thinking, "The damned ne’er-do-well is drunk day and night and brings great shame to my family! I will lose my job and my reputation because of this sluggard!"
    
    "I would very much like to help you, Magistrate Kang...," Gee Soo started his excuse but was immediately interrupted.
    
    "And you will! You do remember last year when you were falsely accused of dishonoring Magistrate Lee’s daughter. It was I who helped justice triumph! Do I ask too much in return?"
    
    To that, Kim Gee Soo replied:
    
    "No, honorable and just magistrate!" He bowed and added through his teeth, "It would be a great honor to help your respectable heir!" and thought to himself, "Whose reputation is legendary in the capital’s brothels, or so I hear."
    
    "Besides, after you perform this noble and heroic deed, generations of kwangdes will praise your glory," said the magistrate putting much effort in hiding his smile of relief.
    
    "As if these damned shrill, garish jesters don’t already tell all sorts of cock-and-bull stories about me!" thought Gee Soo, bowing even lower to hide his infuriated face.
    
    And so, slowly but surely he was approaching his final destination in the hopes to find out why all of the cursed maiden’s suitors were found dead. Gee Soo had to lift the curse so that the magistrate’s son could ask her hand and finally stop consuming all of the capital’s liquor and tarnishing his fathers’ reputation. The hwarang had no idea how but he had to do his best. He was going to be mocked by his fellow hwarangs so the least he could do was try to succeed in this embarrassing task. The mood overwhelming him was as dark as the landscape was beautiful. Suddenly, he heard shrill cries that pulled him back from his thoughts.
    
    <i>Pick a choice at random to proceed:</i>
    
    + [Choice one.]
        
        -> bandits
    
    + [Choice two.]
        
        -> old_monk
    
    + [Choice three.]
        
        -> children_dog
    -

-> END

= bandits
#2

    Suddenly, a few bearded, tattered and visibly unclean men jumped out of the high grass, armed with cudgels, axes, butcher knives and one of them even had a rusty sword. Two poorly crafted bows readied with arrows were pointed at Kim Gee Soo. The men were trying very hard to look scary and they were obviously taking themselves seriously. Kim Gee Soo did not want to disparage their hard-trained menacing poses, so he stifled his laughter behind his hand and stood still. The men took his gesture as a sign of fear and started yelling unintelligibly to intimidate him further. The hwarang had to use both his hands now to stop himself from bursting out. One of the riffraff, probably their leader, approached Kim Gee Soo and barked:
    
    "Come on rich boy, hand over your money and weapons, if you want to live!"
    
    The others drew closer forming a circle around the hwarang. Apparently, he was ambushed by a bunch of bandits. Kim Gee Soo spread his arms in a friendly gesture.
    
    "OK, boys, about the money – it’s not much but you can have it."
    
    "Give us your weapon first!" snapped the leader.
    
    "All right, all right! Everybody just stay calm!"
    
    Hwarang drew his sword and threw it on the ground. The bandit picked it up. In the meantime, Gee Soo already had his bow in one hand and his money pouch in the other. He then threw the open pouch in the bandit’s face who instinctively hid his face with his hands.
    
    <i>Which one of the hwarang's abilities do you want to use:</i>
    
    + [<{HasAbility(sword)}> Sword Mastery.]
        
        #5
        
        ~ AdjustKiEnergy(sword)
        
        At that moment, the hwarang lunged forward, grabbed his sword, rolled over and hit the bandit in the stomach with the sword’s handle. With a mighty swing of his arm, Gee Soo threw his weapon in the air. The thieves looked up frozen in surprise, watching the flying sword. At that moment the hwarang became a whirlwind of movements, as he knocked the bows out of the bowmen’s hands with his scabbard. He rolled back, stretched his arm theatrically and caught the falling sword. With a lightning fast movement, he pressed his blade against the bandit’s throat, trimming a few inches off his beard. Despite the dirt covering his face, the ill-fated thief was visibly white with fear.
    
    + [<{HasAbility(bow)}> Bow Mastery.]
    
        #6
        
        ~ AdjustKiEnergy(bow)
        
        Fast as lighting, Gee Soo took out an arrow from his quiver, got on one knee and loosed it just as one of the archer was making his shot. The hwarang’s arrow split the enemy’s in two and knocked the thief hat right off his head. Gee Soo rolled forward and shot the other archer’s hat off too. The hwarang stood up, readied a third arrow, and held it just a few inches from the paralyzed leader’s face.
        
        "This type of arrow is called ‘battering ram’", said the hwarang. "Its tip weighs about half a pound. This arrow can pierce a reinforced wooden door, leaving a hole big enough to put your arm through. Imagine what it could do to your head if you or one of your buddies move."
    
    + [<{HasAbility(shamanism)}> Shamanism.]
        
        #7
        
        ~ AdjustKiEnergy(shamanism)
        
        Before anyone knew what was happening, Gee Soo cast a simple spell. Within seconds, the moisture evaporating from the wet ground in a ten-yard radius turned into a mist so thick that the attackers could not see their hands in front of their faces. The spell held only for a few seconds but it was enough time for the hwarang. He disarmed the archers, grabbed his sword from the shocked leader’s hands, pulled it out of the scabbard and when the mist cleared, Gee Soo’s blade was pressed against the thief’s throat.
        
        "Magic!"
        
        "Indeed!" confirmed the hwarang.
    
    + [If he does not have these abilities, or you do not want him to use them.]
        
        #9
        
        Gee Soo became a whirlwind of movements. The hwarang lunged forward, grabbed his sword, rolled over and hit the bandit in the stomach with the sword’s handle. He then grabbed him and hurled him towards the two archers. Using the momentum, the hwarang plunged towards the bandits. At that moment, they realized they were in serious trouble and ran head over heels in different directions. Gee Soo turned to face the leader and the two archers that he had knocked down but they had already got on their feet and run away. The hwarang collected his scattered coins from the ground and continued on his way. It would be a while before these unfortunate thieves dared to come back here.
    -
    
    + [Continue.]
    
    -
    
    #96
    
    "Well, buddy! You wanted my money and weapon – you got them. Need anything else?" asked the hwarang in a cold voice.

    "Well, sir… um, there seems to be a mistake! Um, you see, we thought you’re a bandit! You see, they say bandits are roaming the area and uhmmmm... you see, me and the boys decided to scare them away. Uhmmm, you know, show them how bad it is to rob honorable travelers and people from the village. But, you see, it seems we’ve made a mistake!"
    
    The others started nodding vigorously.
    
    "Aha! I see! So, you think I’m a bandit, then? A wretched thief? Is that what you think?" roared Kim Gee Soo.
    
    "N-n-n-no! No, no, no, no, no! Nothing like that, sir! W-we couldn’t see you well from the grass and besides, I’m short-sighted and cross-eyed!" the bandit crossed his eyes as if there was a fly on his nose. "L-l-look, you’ve accidentally dropped your coins...uhmmmm, please, put your weapon down so I can help you collect them!" he said, still having his eyes crossed.
    
    The hwarang sheathed his sword and the bandit dropped on all fours and started picking up the coins.
    
    "They were exactly thirty-two.", Gee Soo informed the panicked thief. In ten seconds, the man collected the coins, gave them a quick polish and put them in Gee Soo’s pouch with trembling hands as if the coins were hot.
    
    "There we go, good sir! Exactly thirty-two coins! Please excuse us once again! Please let us go!" the bandit began to step back slowly.
    
    Gee Soo let him make two steps and shouted, "Stop!" The man halted. "I thought you said you were cross-eyed!"
    
    "I-I-I was actually looking sideways, sir," he stammered with fear.
    
    "I just wanted to tell you, boys, that I’m hwarang Kim Gee Soo – you’ve probably heard of me. I happen to have some business in the village, so I’ll be around. You don’t have to worry about those bandits anymore. Now that I know of them, I’ll look into the matter myself. So you can go home and rest. If you happen to meet those ne’er-do-wells – leave them be. Just tell them that if they want to live, they’d better leave this place for I’m coming for them!"
    
    "Y-yes, brave hwarang! We shall do so! Right, boys?"
    
    "Yes! Yes!" the others repeated with trembling voices.
    
    "Good boys!"
    
    "Uhmmm, sir... one more thing..."
    
    "Yes?"
    
    "Well, we...uhmmm, wanted to offer you a little help in your honorable task and... as a sign of apology, we would like to give you this modest gift!"
    
    + [<{HasAbility(luck)}> If hwarang has the ability Luck and you want him to use it.]
        
        #10
        
        ~ AdjustKiEnergy(luck)
        
        The bandit took out a small pouch made of thick leather.

        "A good monk gave it to us. If you throw it hard enough at something, it goes ‘KABOOM’ and turns it into a cloud of smoke and dust! Please, take it."
        
        "Thanks! Now beat it!"
        
        "Sure thing, sir, we’re off! See you la... I mean goodbye!"
        
        The bandits backed away slowly for a few yards and then ran head over heels.
        
        Kim Gee Soo finally burst into laughter. It took him a bit to calm down before continuing down the road.
        
        -> AddCodeword(ember) ->
        
        ++ [Move on.]
        
    + [Otherwise.]
    
        #11
        
        The bandit took out a small clay flask.

        "A good monk gave it to us. It clears the mind, this stuff. Please, take it!"
        
        "Thanks! Now beat it!"
        
        "Sure thing, sir, we’re off! See you la... I mean goodbye!"
        
        The bandits backed away slowly for a few yards and then ran head over heels.
        
        Kim Gee Soo finally burst into laughter. It took him a bit to calm down before continuing down the road.
    
        -> AlterStats(mind, "Mind", 1) ->
        
        ++ [Move on.]
        
    -
    
    -> village

-> END

= old_monk
#3
    
    "Leave me be, damn you!" he heard an old frail voice. "No! No! Not the scroll! You'll ruin it, you scoundrels! What do you need it for? I bet you can't even read!"
    
    A group of men burst into a tumultuous laughter and then a coarse, raspy voice said:
    
    "We can't read, old man, but it looks valuable. We'll get good money for of it!"
    
    "It's valuable to me, not to you, you mindless goons! May you all reincarnate into dogs, you..." the curse ended with a pitiful cry.
    
    "Shut it, old fool! Or you'll be counting the worms sooner than you think!"
    
    "You'll be counting them too sooner or later, you idiots! Buddha will judge you for your crimes! NO! Careful with that potion! Arghhhh! May you break your leg!"
    
    "You don't shut up, and I'll break it in your bony ass, old-timer!"
    
    Kim Gee Soo climbed the small hill and witnessed a strange view that in other circumstances would have been funny. Six tattered men armed with primitive weapons had formed a half-circle around an old wandering monk and were tossing a rolled up scroll to each other. The old man was jumping and waving his bony hands in an unsuccessful attempt to catch the scroll. The monk would not stop cursing, which earned him an occasional kick in the rear. This band of thieves was robbing the poor man. The hwarang had to help him.
    
    His position was advantageous as he was standing behind the half-circle of bandits.
    
    "Give me back my possessions, you fools! I’m an enchanter monk! I’ve spent years studying the esoteric mysteries!" The monk stopped and waved his fist threateningly, "If you don’t come to your senses I’ll summon upon you demons from the seventh circle of hell!"
    
    The thieves burst out laughing.
    
    "OK, then! You asked for it!" the monk raised his hands to the sky and began to chant:
    
    "Shinayanthra, maharava, omnavashyvaya!"
    
    <i>Do you want the hwarang to use any of these abilities:</i>
    
    + [<{HasAbility(sword)}> Sword-mastery.]
        
        #13
        
        ~ AdjustKiEnergy(sword)
        
        Gee Soo rushed towards the closest bandit, who had raised his cudgel to bludgeon the mouthy monk. The hwarang kicked him behind the knee and the thief fell with a scream of surprise. With two strikes of his sword, he knocked the monk’s belongings off the puzzled thugs’ hands and with two more, he disarmed the others.
    
    + [<{HasAbility(bow)}> Bow-mastery.]
    
        #14
        
        ~ AdjustKiEnergy(bow)
        
        Gee Soo took aim and made a shot at the closest bandit, who had just raised his cudgel to bludgeon the mouthy monk. The hwarang hit him behind the knee and the thief fell with a surprised scream. With two more shots, he knocked the monk’s belongings off the puzzled thugs’ hands.
    
    + [<{HasAbility(shamanism)}> Shamanism.]
        
        #15
        
        ~ AdjustKiEnergy(shamanism)
        
        Gee Soo cast a simple spell – he conjured up mist around the bandits. He could only hold it for a few seconds but that was enough time for him.
    
    + [If he does not have these abilities or you do not want him to use them.]
        
        #13
        
        -> AddCodeword(theft) ->
        
        Gee Soo rushed towards the closest bandit, who had raised his cudgel to bludgeon the mouthy monk. The hwarang kicked him behind the knee and the thief fell with a scream of surprise. With two strikes of his sword, he knocked the monk’s belongings off the puzzled thugs’ hands and with two more, he disarmed the others.
    
    -
    
    + [Continue.]
    
    -
    
    #97
    
    The bandits looked at him with pure terror and took to their heels as if their lives depended on it. Gee Soo turned towards the thief he had knocked down but he was already on his feet and limping away, screaming at the top of his lungs, "DEEEMOOOON!"

    The old man was still standing with his arms up, staring at the hwarang. Gee Soo put away his weapon and approached the monk.
    
    "Don’t come any closer, demon!" shouted the old man and took two steps back. "Go back to the hell pit you crawled out of, you wretch!"
    
    "Honorable monk, I’m hwarang Kim Gee Soo..."
    
    "Stay where you are!" squawked the monk. "Kim Gee Soo… never heard of such a demon before!"
    
    "I’m not a demon, old man!" the hwarang replied insulted. "I am hwarang Kim Gee Soo!"
    
    "Pull out your tongue!"
    
    "I beg your pardon?"
    
    "Do it! If you’re a demon your tongue will be purple!"
    
    Kim Gee Soo looked around carefully and when he was sure there was no one else to witness his humiliation, pulled out his tongue to the crazy monk’s satisfaction.
    
    "There! Are you convinced now?"
    
    The old man gave him a look of disappointment.
    
    "And I thought that my incantation worked," he said, disheartened, and finally put his arms down.
    
    "I was passing by, heard your screams and decided to help you, as the hwarang code demands. Why do you insult me so?"
    
    The monk smoothed his clothes back into place in an unsuccessful attempt to regain self-control.
    
    "Honorable hwarang," he bowed low, "I am Pak Yon Wol – a wandering monk, adept at the esoteric secrets... well... obviously not so adept after all. Please accept my deepest apologies for calling you... you know... I mean... these scoundrels scared me a bit and... I wasn’t myself that’s all..." the old man blushed from embarrassment.
    
    + [<{codewords has theft}>If you have the code word <b>theft</b>.]
    
        #17
        
        "Unfortunately, those idiots, may Buddha curse them with a thousand reincarnations into worms, took all my possessions and I have nothing to repay my debt. Perhaps you can spare some of your time and allow me to share some secrets that I’ve learned through the years about the esoteric and supernatural. For beginners, of course!"

        Regretfully, his code of honor obliged Kim Gee Soo to agree and he spent the next half hour patiently listening to the monk’s ramblings. Nonetheless, he had to admit that he learned a few interesting things.
        
        After the lesson was over, the old man thanked him again and went on his way, cursing the thieves quietly. The hwarang continued on his way to the village.
        
        -> AlterStats(knowledge, "Knowledge", 1) ->
        
        ++ [Move on.]
    
    + [<{HasAbility(luck)}>If you don’t have that code word, but the hwarang has the ability Luck and you want him to use it.]
        
        #18
        
        ~ AdjustKiEnergy(luck)
        
        "The scroll that the scoundrels tried to steal from me," said the monk reluctantly, "is very valuable for it contains several basic tractates on the esoteric. I give it to you because I know you will to appreciate its worth!"

        "I can't accept such a precious gift from you, generous monk. I'm not worthy of such wisdom," replied the hwarang overenthusiastically because he didn't feel like becoming the new owner of a scroll full of boring inane writings, let alone reading it.
        
        "You will accept it!" snapped the old man. "Otherwise I'll get bad karma because of you! I know every word in it by heart! Do you want to stop my next reincarnation?"
        
        "Not at all, devout monk!" unwillingly replied Gee Soo. "It is a great honor to accept this valuable source of wisdom and knowledge from you! May Buddha make you a Bodhisattva in your next life for your kindness," he said ceremonially and took the scroll with both hands from the monk.
        
        "May Buddha watch over the success of your endeavors, brave hwarang!" replied the monk.
        
        After that, he thanked him once again and continued on his way, cursing the thieves quietly. The hwarang headed to the village.
        
        -> AlterStats(knowledge, "Knowledge", 2) ->
        
        ++ [Move on.]
    
    + [If you don’t have that code word and Gee Soo doesn’t have that ability or you don’t want him to use it.]
        
        #19
        
        "The potion in the flask that the thieves tried to steal from me," said the monk reluctantly, "is made from secret ingredients that clear the mind. I give it to you because I know you'll appreciate its worth!"

        "I can't accept a gift of such value, generous monk," replied the hwarang overenthusiastically because he did not really want to carry around suspicious potions, let alone drink them.
        
        "You can and will!" snapped the monk. "Otherwise, I'll get bad karma because of you! Do you want to stop my next reincarnation?"
        
        "Not at all, devout monk!" unwillingly replied Gee Soo. "It is a great honor to accept this enlightening gift from you! May Buddha make you a Bodhisattva  in your next life for your kindness!" he said ceremonially and took the flask with both hands from the monk.
        
        "May Buddha watch over the success of your endeavors, brave hwarang!" replied the monk. "Now drink!"
        
        "Well... I thought I’d save it for emergencies..."
        
        "Drink!" snapped the old man.
        
        Gee Soo removed the plug, held his breath, mustered his courage and poured the liquid in his mouth. The long hours of hwarang training in self-control and iron will stopped him from emptying his stomach on the monk's feet. The liquid tasted like week-old dead rats.
        
        "Do you feel the rush, son?" asked the monk eagerly.
        
        "Mhmm!" was the only thing Gee Soo could mumble. He did not feel the rush yet, but the urge to throw up was still too strong to risk opening his mouth.
        
        "Do you feel your mind reaching nirvana?"
        
        "Mhmm!"
        
        "Well, I thank the gods you saved me from those thieves! Be in peace and may Buddha give you strength and guide your mind," said the monk and prepared to leave.
        
        "Mhmm," replied the hwarang.
        
        The monk thanked him once again and continued on his way, cursing the thieves quietly, and Gee Soo headed to the village. Soon the foul taste in his mouth disappeared and he was surprised to realize that his mind did feel very clear. He suspected the effect was due to the terrible taste of the potion and not its ingredients.
        
        -> AlterStats(mind, "Mind", 1) ->
        
        ++ [Move on.]
        
    -
    
    -> village
    
-> END

= children_dog
#4
    The hwarang heard children laughing and a dog’s whining that turned to muffled barking only to escalate to a loud whining again. He fastened his pace and down the road, he saw a chilling picture. A gang of rascals had cornered a scrawny dog next to a rock and were shooting pebbles at the animal with slings. The poor creature was darting left and right, trying to evade the volleys of pebbles but it could not run past the children as they had formed a half-circle around it. On top of that, they had somehow managed to tie its tail to one of its hind legs. The hwarang’s heart filled with righteous anger and he rushed towards the little sadists to bring justice on them.
    
    "Hey, you little brats! Stop this at once! Why are you torturing this animal?"
    
    The kids did not bother to answer and shot another volley of pebbles at the dog, which had given up attempting to escape and had coiled up in a ball, pressed against the rock.
    
    "Scram or I'll tear your ears off!" Gee Soo barked.
    
    In response, one stocky kid turned to him and tried to shoot him with his sling. The hwarang dodged the poorly aimed shot and jumped towards the rascals. Sensing what was about to happen, they immediately dispersed and darted towards the village.
    
    "Get back here, you imps! You can't hide from me! I'll find out who you are and see that you get what you deserve!" Kim Gee Soo shouted as he reached the rock.
    
    The dog had come to its senses and was watching its savior with a mix of wonder and gratitude.
    
    "Eiishhhh," the hwarang blurted one last curse and looked at the dog which whimpered pitifully and moved towards him. He knelled and untied its tail while the animal was licking his hand with gratitude.
    
    "OK, OK! You don't have to thank me! Go on now and stay away from kids! They only seem harmless."
    
    He stood up and continued walking but after a few steps, he realized the dog was following him.
    
    "Oh, no! Come on now! Shoo!" The dog did not move. "Go away! Beat it!"
    
    It looked at him guiltily and started licking his hands again.
    
    "Cut it out! What am I to do with you? You're lucky I don't eat dogs! If you want to follow me, be my guest but I’m telling you, I won't feed you and you’re on your own. Let's see how long you'll last."
    
    The animal barked with happiness and peed the grass.
    
    "Great! From now on I'll probably also be known as the Dog Master or the Defender of the Four-Legged!" he growled and continued to the Yo Soo village, his new friend following him.
    
    -> AddCodeword(dog) ->
    
    + [Move on.]
    
        -> village

-> END